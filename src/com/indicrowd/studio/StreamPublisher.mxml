<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
				xmlns:s="library://ns.adobe.com/flex/spark"
				xmlns:mx="library://ns.adobe.com/flex/mx"
				width="480" height="360" minWidth="480" minHeight="360" horizontalScrollPolicy="off"
				initialize="init()" layout="absolute" verticalScrollPolicy="off">
	<fx:Script>
		<![CDATA[
			import flash.external.*;
			import mx.rpc.events.ResultEvent;
			import flash.media.scanHardware;
			
			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.events.SliderEvent;
			import mx.events.ToolTipEvent;
			
			[Bindable]
			private var serverInfoURL:String = "/server/streaming/optimal.json";
			private var urlPrefix:String = "/IndiCrowd";
			
			private var nc:NetConnection = null;
			private var camera:Camera;
			private var microphone:Microphone;
			private var nsPublish:NetStream = null;  
			private var connStr:String;
			[Bindable]
			private var pubStreamStr:String;
			
			
			private var camWidth:uint = 480;
			private var camHeight:uint = 360;
			private var camFPS:uint = 20;
			private var camQuality:uint = 100;
			private var camOnline:Boolean = true;
			
			private var micRateSel:Array = [ 5, 8, 11, 22, 44 ];
			private var micRate:uint = 4;
			private var micEncodeQuality:uint = 10;
			private var micPlay:Boolean = true;
			private var micCodec:String = SoundCodec.SPEEX;
			
			private var handleUpdateCamValue:uint;
			private var handleUpdateMICValue:uint;
			
			private var openSetting:Boolean = false;
			
			[Bindable]
			private var acCameraData:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			private var acMICData:ArrayCollection = new ArrayCollection();
			
			
			/**
			 * 전송 스트림 이름 설정 
			 */
			public function setPubStreamStr(pubstr:String):void
			{
				pubStreamStr = pubstr;
			}
			
			
			/**
			 * 전송 스트림 이름 가져오기  
			 */
			public function getPubStreamStr():String
			{
				return pubStreamStr;
			}
			
			
			/**
			 * 어플리케이션이 시작되면 무조건 호출되는 함수 
			 * 
			 */
			public function init():void
			{
				//flash.media.scanHardware();
				var initSuccess:Boolean = startCamera();
				
				if (!initSuccess)
				{
					btnStartPublish.enabled = false;
				}
				
				// 시작 변수 받아오기 
				pubStreamStr =  FlexGlobals.topLevelApplication.parameters.pubStreamStr;
				urlPrefix =  FlexGlobals.topLevelApplication.parameters.urlPrefix;
				serverInfoURL = urlPrefix + serverInfoURL;
				
				// 외부 자바스크립트 호출 함수 등록  
				ExternalInterface.addCallback("setPubStreamStr", setPubStreamStr);
				ExternalInterface.addCallback("getPubStreamStr", getPubStreamStr);
				
				serverInfoService.send();
			}
			
			/**
			 * 방송하기 버튼을 클릭하면 호출되는 함수
			 * @event 마우스 클릭 이벤트 변경 
			 */
			private function doLive(event:MouseEvent):void
			{
				if (btnStartPublish.label == "방송하기")
				{
					startLive();
				}
				else
				{
					stopLive();
				}
			}
			
			
			/**
			 * 방송 시작 
			 * 
			 */
			private function startLive():void
			{
				startPublish();
				btnStartPublish.label = "방송중지";
			}
			
			/**
			 *  방송 정지 
			 */
			private function stopLive():void
			{
				stopPublish();
				btnStartPublish.label = "방송하기";
			}
			
			private function scanCamera():void
			{
				acCameraData.removeAll();
				
				var count:int = 0;
				
				for(var i:uint; i < Camera.names.length; i++)
				{
					var cam:Camera = Camera.getCamera( String(i) );
					acCameraData.addItem( { label:cam.name, data:String(i) });
				}
				
				trace("Camera List : " + Camera.names.toString());
			}
			
			private function scanMIC():void
			{
				acMICData.removeAll();
				
				var count:int = 0;
				
				for(var i:uint; i < Microphone.names.length; i++)
				{
					var mic:Microphone = Microphone.getMicrophone(i);
					acMICData.addItem( { label:mic.name, data:String(i) });
				}
			}
			
		
			private function startCamera():Boolean
			{	
				// get the default Flash camera and microphone
				camera = Camera.getCamera();
				microphone = Microphone.getMicrophone();
				
				// here are all the quality and performance settings that we suggest
				if(camera != null)
				{
					changeCamera(0);
				}
				else
				{
					//Alert.show("카메라가 설치되어 있지 않습니다.");
					return false;
				}
				if( microphone != null)
				{
					changeMIC(0);
				}
				else
				{
					//Alert.show("마이크가 설치되어 있지 않습니다.");
					return false;
				}
				
				return true;
			}
			
			
			private function changeCamera(index:int = -1):void 
			{
				if (index != -1)
				{
					camera = Camera.getCamera(String(index));
					
					camera.setMode(camWidth, camHeight, camFPS, false);
					camera.setQuality(0, camQuality);
					camera.setKeyFrameInterval(30);
		
					
					videoCamera.attachCamera(camera);
					
					if (nsPublish != null)
						nsPublish.attachCamera(camera);
				} 
				else 
				{
					camera.setMode(camWidth, camHeight, camFPS, false);
					camera.setQuality(0, camQuality);
					camera.setKeyFrameInterval(30);
				}
				
			}
			
			
			private function changeMIC(index:int = -1):void 
			{
				if (index != -1)
				{
					microphone = Microphone.getMicrophone(index);
					
					 
					microphone.codec = micCodec;
					microphone.rate = micRateSel[micRate];
					microphone.encodeQuality = micEncodeQuality;
					microphone.setSilenceLevel(0); 
					
					
					if (nsPublish != null)
						nsPublish.attachAudio(microphone);	
				}
				else 
				{
					microphone.rate = micRateSel[micRate];
					microphone.encodeQuality = micEncodeQuality;
					microphone.setSilenceLevel(0); 
				}
				
			}
			
			private function ncOnStatus(infoObject:NetStatusEvent):void
			{
				trace("nc: "+infoObject.info.code+" ("+infoObject.info.description+")");
				
				if (infoObject.info.code == "NetConnection.Connect.Failed")
				{
					// 접속 실패
				} else if (infoObject.info.code == "NetConnection.Connect.Rejected") {
					// 연결 거부
				}
			}
			
			
			private function connect():void
			{
				// connect to the Wowza Media Server
				if (nc == null)
				{
					// create a connection to the wowza media server
					nc = new NetConnection();
					nc.connect(connStr);
					
					// get status information from the NetConnection object
					nc.addEventListener(NetStatusEvent.NET_STATUS, ncOnStatus);
					
				}
			}
			
			private function disconnect():void
			{
				nsPublish = null;
				
				nc.close();
				nc = null;
			}
			
			
			// function to monitor the frame rate and buffer length
			private function updateStreamValues():void
			{
				if (fpsText == null || bufferLenText == null)
					return ;
				
				if (nsPublish != null)
				{
					fpsText.text = (Math.round(nsPublish.currentFPS*1000)/1000)+" fps";
					bufferLenText.text = (Math.round(nsPublish.bufferLength*1000)/1000)+" secs";
				}
				else
				{
					fpsText.text = "";
					bufferLenText.text = "";
				}
			}
			
			private function updateMICValues():void
			{
				if (soundLevel == null)
					return ;
				
				if (microphone != null)
					soundLevel.text = String(microphone.activityLevel);
				else
					soundLevel.text = "";
			}
			
			private function nsPublishOnStatus(infoObject:NetStatusEvent):void
			{
				trace("nsPublish: "+infoObject.info.code+" ("+infoObject.info.description+")");
				//if (infoObject.info.code == "NetStream.Play.StreamNotFound" || infoObject.info.code == "NetStream.Play.Failed")
				//prompt.text = infoObject.info.description;
			}
			
			private function startPublish():void
			{
				// create a new NetStream object for video publishing
				nsPublish = new NetStream(nc);
				
				nsPublish.addEventListener(NetStatusEvent.NET_STATUS, nsPublishOnStatus);
				
				// set the buffer time to zero since it is chat
				nsPublish.bufferTime = 0;
				
				var h264Settings:H264VideoStreamSettings = new H264VideoStreamSettings();
				h264Settings.setProfileLevel(H264Profile.BASELINE, H264Level.LEVEL_3);
				h264Settings.setQuality(0, camQuality);
				h264Settings.setKeyFrameInterval(15);
				h264Settings.setMode(camWidth, camHeight, camFPS);
				nsPublish.videoStreamSettings = h264Settings;
				
				// publish the stream by name
				nsPublish.publish(pubStreamStr, "append");
				
				// add custom metadata to the stream
				var metaData:Object = new Object();
				metaData["description"] = "Chat using VideoChat example."
				nsPublish.send("@setDataFrame", "onMetaData", metaData);
				
				// attach the camera and microphone to the server
				if (camOnline)
					nsPublish.attachCamera(camera);
				
				if (micPlay)
					nsPublish.attachAudio(microphone);
			}
			
			private function stopPublish():void
			{
				nsPublish.attachCamera(null);
				nsPublish.attachAudio(null);
				nsPublish.publish("null");
				nsPublish.close();
				nsPublish = null;
			}
			
			protected function listCamera_initializeHandler(event:FlexEvent):void
			{
				scanCamera();
			}
			
			protected function listCamera_changeHandler(event:ListEvent):void
			{
				changeCamera(event.currentTarget.selectedIndex);
			}
			
			protected function listMIC_initializeHandler(event:FlexEvent):void
			{
				scanMIC();
			}
			
			protected function listMIC_changeHandler(event:ListEvent):void
			{
				changeMIC(event.currentTarget.selectedIndex);
			}
			
			protected function slideCamQuality_changeHandler(event:SliderEvent):void
			{
				camQuality = slideCamQuality.value;
				
				changeCamera();
			}
			
			protected function slideCamQuality_initializeHandler(event:FlexEvent):void
			{
				slideCamQuality.value = camQuality;
			}
			
			protected function slideMICQuality_changeHandler(event:SliderEvent):void
			{
				micRate = slideMICQuality.value;
			
				changeMIC();
			}
			
			protected function slideMICQuality_initializeHandler(event:FlexEvent):void
			{
				slideMICQuality.value = micRate;
			}
			
			private function micToolTipFunc(val:String):String {
				switch(int(val))
				{
					case 0:
						return "최저품질";
						break;
					case 1:
						return "저품질";
						break;
					case 2:
						return "중간품질";
						break;
					case 3:
						return "고품질";
						break;
					case 4:
						return "최고품질";
						break;			
				}
				
				return "";
			}
			
			
			/* 캠 퀄리티 자동으로 변경 */
			protected function cbCamQualityAuto_changeHandler(event:Event):void
			{
				if(cbCamQualityAuto.selected)
				{
					camQuality = 0;
					slideCamQuality.enabled = false;
				} else {
					camQuality = slideCamQuality.value;
					slideCamQuality.enabled = true;
				}
				
				changeCamera();
			}
			
			
			protected function cbCameraHD_changeHandler(event:Event):void
			{
				if (cbCameraHD.selected)
				{
					camHeight = 720;
					camWidth = 1280;
				} else {
					camHeight = 360;
					camWidth = 480;
				}
				changeCamera();
			}
			
			protected function cbMicPlay_changeHandler(event:Event):void
			{
				micPlay = cbMicPlay.selected;
				
				if (nsPublish != null)
				{
					if (micPlay) 
					{
						nsPublish.attachAudio(microphone);
					}
					else 
					{
						nsPublish.attachAudio(null);
					}
				}
				
			}
			
			protected function cbCamOnline_changeHandler(event:Event):void
			{
				camOnline = cbCamOnline.selected;
				
				if (nsPublish != null)
				{
					if (camOnline) 
					{
						nsPublish.attachCamera(camera);
					}
					else 
					{
						nsPublish.attachCamera(null);
					}
				}
			}
			
			
			protected function btnSetting_clickHandler(event:MouseEvent):void
			{
				openSetting = !openSetting;
				
				if (openSetting)
				{
					this.width = 480;
				} else {
					this.width = 800;	
				}
				flash.external.ExternalInterface.call("resizeStreamPublisher", this.width, this.height);
			}
			
			
			private function readServerInfo(event:ResultEvent):void
			{				
				var result:Object = JSON.parse(event.result as String);
				
				connStr = "rtmp://" + result.command.hostname + ":" + result.command.rtmpPort + "/live";
				
				connect();
				
				// 성능 주기적으로 출력 
				handleUpdateCamValue = setInterval(updateStreamValues, 500);
				handleUpdateMICValue = setInterval(updateMICValues, 100);
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<mx:HTTPService id="serverInfoService" url="{serverInfoURL}" useProxy="false" method="GET" result="readServerInfo(event)" showBusyCursor="true">
			<mx:request xmlns="">
			</mx:request>
		</mx:HTTPService>
	</fx:Declarations>
	
	<mx:VideoDisplay id="videoCamera" x="0" y="0" width="480" height="360" autoPlay="false"/>
	<mx:TabNavigator x="480" y="0" width="320" height="360">
		<mx:Canvas width="100%" height="100%" label="하드웨어 설정">
			<mx:Label x="10" y="10" text="카메라 : "/>
			<mx:Label x="10" y="62" text="카메라 품질 : "/>
			<mx:Label x="10" y="88" text="마이크 품질 : "/>
			<mx:Label x="10" y="36" text="마이크 :"/>
			<mx:ComboBox id="listCamera" y="4" left="81" width="160"
						 change="listCamera_changeHandler(event)" dataProvider="{acCameraData}"
						 initialize="listCamera_initializeHandler(event)"></mx:ComboBox>
			<mx:ComboBox id="listMIC" y="34" left="81" width="160"
						 change="listMIC_changeHandler(event)" dataProvider="{acMICData}"
						 initialize="listMIC_initializeHandler(event)"></mx:ComboBox>
			<mx:HSlider id="slideCamQuality" x="81" y="62"
						change="slideCamQuality_changeHandler(event)"
						initialize="slideCamQuality_initializeHandler(event)" maximum="100"
						minimum="1" tickInterval="20"/>
			<mx:HSlider id="slideMICQuality" x="81" y="90"
						change="slideMICQuality_changeHandler(event)"
						initialize="slideMICQuality_initializeHandler(event)" maximum="4"
						minimum="0" snapInterval="1" tickValues="[0,1,2,3,4] "
						dataTipFormatFunction="micToolTipFunc" />
			<mx:CheckBox id="cbCameraHD" x="249" y="6" label="HD"
						 change="cbCameraHD_changeHandler(event)"/>
			<mx:CheckBox id="cbCamQualityAuto" x="249" y="61" label="자동"
						  change="cbCamQualityAuto_changeHandler(event)" />
			<mx:CheckBox id="cbMicPlay" x="62" y="36" change="cbMicPlay_changeHandler(event)"
						 selected="true"/>
			<mx:CheckBox id="cbCamOnline" x="62" y="8" change="cbCamOnline_changeHandler(event)"
						 selected="true"/>
		</mx:Canvas>
		<mx:Canvas width="100%" height="100%" label="디버깅 콘솔">
			<mx:Label id="fpsText" x="57" y="17" width="75"/>
			<mx:Label id="bufferLenText" x="57" y="43" width="75"/>
			<mx:Label id="soundLevel" x="57" y="69" width="75"/>
		</mx:Canvas>
	</mx:TabNavigator>
	<mx:Canvas x="0" y="328" width="480" height="32" alpha="1.0" backgroundAlpha="0.0">
		<mx:Button id="btnStartPublish" x="10" y="5" label="방송하기" click="doLive(event)"/>
		<mx:Button id="btnSetting" x="398" y="5" label="설정" click="btnSetting_clickHandler(event)"/>
	</mx:Canvas>
	
</mx:Application>
